import org.jetbrains.intellij.tasks.RunPluginVerifierTask

plugins {
    id "idea"
    id "java"
    id 'org.jetbrains.intellij' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.32'
}

ext {
    kotlin_version = '1.4.32'
}

// The latest supported versions
def IJ_VERSION = '2021.2'
def GO_PLUGIN_VERSION = '212.4746.92'
// Unfortunately the GoLand releases do not completely match the Go plugin releases
def GOLAND_VERSION = '2021.2'
// Test that the plugin still works on old releases
def IJ_FROM_VERSION = '2020.3'
def GOLAND_FROM_VERSION = '2020.3'

group 'com.squareup.cash.hermit'
version project.getProperties().getOrDefault("version", '1.0-SNAPSHOT')

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions.freeCompilerArgs = ["-Xjvm-default=enable"]
}

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'
}

intellij {
    // Note: The IntelliJ version below needs to match the go plugin version as defined here:
    // https://plugins.jetbrains.com/plugin/9568-go/versions
    version = IJ_VERSION
    type = 'CE'
    plugins = ["gradle", "java", "terminal", "org.jetbrains.plugins.go:" + GO_PLUGIN_VERSION]
}

runIde {
    // Uncomment this, and set your path accordingly, if you want to debug on GoLand
    // ideDirectory '/Users/juho/Library/Application Support/JetBrains/Toolbox/apps/Goland/ch-0/203.6682.164/GoLand.app/Contents'
}

patchPluginXml {
    sinceBuild = '203.5981.41'
    version = System.getenv("IJ_PLUGIN_VERSION")
}

runPluginVerifier {
    // These need to match the versions from
    // https://data.services.jetbrains.com/products?fields=code,name,releases.downloads,releases.version,releases.build,releases.type&code=IIC,IIE,GO
    ideVersions = ['IIC-' + IJ_FROM_VERSION, 'IIC-'+ IJ_VERSION, 'GO-' + GOLAND_FROM_VERSION, 'GO-' + GOLAND_VERSION]
    failureLevel = EnumSet.complementOf(
            EnumSet.of(
                    // skipping missing dependencies as com.intellij.java provided bi IJ raises a false warning
                    RunPluginVerifierTask.FailureLevel.MISSING_DEPENDENCIES
            )
    )

}

publishPlugin {
    token = System.getenv("JETBRAINS_TOKEN")
}

apply plugin: 'kotlin-kapt'

def arrow_version = "0.11.0"
dependencies {
    implementation "io.arrow-kt:arrow-core:$arrow_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    kapt    "io.arrow-kt:arrow-meta:$arrow_version"
}

// See https://youtrack.jetbrains.com/issue/KTIJ-782
tasks.buildSearchableOptions {
    enabled = false
}
